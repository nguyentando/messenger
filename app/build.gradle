apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    def root = rootProject.ext

    compileSdkVersion root.compileSdkVersion

    defaultConfig {
        applicationId "com.donguyen.messenger"
        minSdkVersion root.minSdkVersion
        targetSdkVersion root.targetSdkVersion
        versionCode root.versionCode
        versionName root.versionName
        testInstrumentationRunner root.test.testInstrumentationRunner
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        test {
            // include the share folder from domain module
            java.srcDirs += "$rootDir/domain/src/share"
        }
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    def core = rootProject.ext.core
    def archComponents = rootProject.ext.architectureComponents
    def support = rootProject.ext.supportLibraries
    def development = rootProject.ext.development
    def test = rootProject.ext.test

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // -------------------------------------- MODULE -----------------------------------------------
    implementation project(':domain')
    implementation project(':data')

    // -------------------------------------- CORE -------------------------------------------------
    implementation core.kotlin
    implementation core.rxKotlin
    implementation core.rxAndroid
    implementation core.ktxCore
    // dagger
    implementation core.dagger
    kapt core.daggerCompiler

    // -------------------------------------- ARCHITECTURE COMPONENTS ------------------------------
    // room
    implementation archComponents.room
    implementation archComponents.roomRxJava
    kapt archComponents.roomCompiler
    // paging
    implementation archComponents.paging
    implementation archComponents.pagingRxJava
    // lifecycle
    implementation archComponents.lifecycle
    implementation archComponents.lifecycleCommonJava

    // for testing
    testImplementation archComponents.archTesting

    // -------------------------------------- SUPPORT LIBRARIES ------------------------------------
    implementation support.appCompat
    implementation support.recyclerView
    implementation support.recyclerViewSelection
    implementation support.materialDesign

    // -------------------------------------- DEVELOPMENT ------------------------------------------
    // glide
    implementation development.glide
    kapt development.glideCompiler

    // -------------------------------------- TESTING ----------------------------------------------
    testImplementation test.junit
    testImplementation test.mockito

    androidTestImplementation test.core
    androidTestImplementation test.extension
    androidTestImplementation test.runner
    androidTestImplementation test.rules
    // espresso
    androidTestImplementation test.espresso
    androidTestImplementation test.espressoContrib
    implementation test.espressoIdlingResource
}
